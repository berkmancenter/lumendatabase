<% require_asset 'select2' %>

selectedField = null

class Redactable
  constructor: (@input) ->

  storeSelection: ->
    $("#selected_text").val(this._selectedText())

  redactSelection: ->
    selectedText = this._selectedText()

    if selectedText
      @input.value = @input.value.replace(
        ///#{this._literal(selectedText)}///g, "[REDACTED]"
      )

      this._clearSelection()

  unredact: ->
    if originalInput = $("##{@input.id}_original")[0]
      @input.value = originalInput.value

  _selectedText: ->
    @input.value.substring(
      @input.selectionStart, @input.selectionEnd
    )

  _literal: (text) ->
    text.replace /([\.\+\*\?\(\[\{\)\]\}])/g, '\\$1'

  _clearSelection: ->
    # prevent double redacting
    @input.selectionStart = 0
    @input.selectionEnd = 0

addHandlers = ->
  $("textarea").each (_, textarea) ->
    $(textarea).focus ->
      selectedField = new Redactable this

  $("#redact-selected").click ->
    selectedField?.redactSelection()

  $("#redact-selected-everywhere").click ->
    selectedField?.storeSelection()

  $("#unredact-selected").click ->
    selectedField?.unredact()

  $('#notice_scopes_in_topics').select2
    placeholder: "Copyright, Trademark"
    width: '50%';

  $('#notice_scopes_submitted_by').select2
    placeholder: "John Doe"
    width: '50%';

requireType = ->
  $('#edit_notice #notice_reset_type + div > input, #new_notice #notice_reset_type + div > input').attr('required', true)

$(document).ready -> addHandlers()
$(document).ready -> requireType()
$(document).on 'pjax:end', -> addHandlers()
$(document).on 'pjax:end', -> requireType()
